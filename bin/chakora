#!/usr/bin/perl -w
#
# /  __ \ |         | |                  
# | /  \/ |__   __ _| | _____  _ __ __ _ 
# | |   | '_ \ / _` | |/ / _ \| '__/ _` |
# | \__/\ | | | (_| |   < (_) | | | (_| |
#  \____/_| |_|\__,_|_|\_\___/|_|  \__,_|
#          Chakora IRC Services
#
# Core file.
use strict;
use warnings;
use IO::Socket;
use Config::Scoped;
use File::Data;
use Getopt::Long;

use lib "../lib";

my (%options);
# Get command line options
GetOptions("help" => \$options{help},
	   "debug" => \$options{debug},
	  );

#Handle --help
if ($options{help})
{
print("**Chakora Help**\n");
print("--debug = Run in debug mode\n");
print("--help = Return this help menu\n");
exit;
}

# Don't allow Chakora to run as root
if (`whoami` eq "root\n") { error("startup", "Do not run Chakora as root"); }

# Get configuration values
my $conf = Config::Scoped->new(
	file => "../etc/chakora.conf",
	) or die("We couldn't open the config file!\n");

# Put them into variables	
my $settings = $conf->parse;

# Create some variables for later use
our %rawcmds;

if (lc(config('server', 'ircd')) eq 'inspircd') 
{
	require Chakora::Protocol::InspIRCd;
} elsif  (lc(config('server', 'ircd')) eq 'charybdis') 
{
	require Chakora::Protocol::Charybdis;
} else 
{
	error("error", "This protocol isn't supported by Chakora.");
}
	
	
# Open the socket and connect to the server
my $socket = IO::Socket::INET->new(
	Proto => "tcp",
	LocalAddr => config('server', 'vhost'),
	PeerAddr => config('server', 'host'),
	PeerPort => config('server', 'port'),
	) or die("Connection to ".config('server', 'host')." failed.\n");

# Create some variables for later use
my ($data, $ex, @ex);

# Connect!
irc_connect();

while ($data = <$socket>) 
{
	
	chomp($data);
	undef $ex;
	@ex = split(' ', $data);
	
	print("[IRC] ".$data."\n");
	
	if ($ex[0] eq 'CAPAB' and $ex[1] eq 'MODULES' and lc(config('server', 'ircd')) eq 'inspircd') {
		# die() if m_invisible is loaded, this will not be removed, so don't ask
		if ($data =~ m/m_invisible.so/) {
			error("chakora", "We forbid the use of Chakora with InspIRCd with m_invisible.so loaded, please unload it then try again!");
		}
		if ($data !~ m/m_services_account.so/) { 
			error("chakora", "Chakora requires that m_services_account be loaded in InspIRCd, please load it and try again!");
		}
	}
	elsif ($ex[0] eq 'CAPAB' and $ex[1] eq 'END' and lc(config('server', 'ircd')) eq 'inspircd') {
		raw_capabend();
	}
	elsif ($ex[0] eq 'PING' and !$ex[2] and lc(config('server', 'ircd')) eq 'charybdis') {
		raw_endsync();
	}

	if ($rawcmds{$ex[1]}{handler}) 
	{
		my $sub_ref = $rawcmds{$ex[1]}{handler};
		eval 
		{
			&{ $sub_ref }($data);
		};
	}
	
}

sub send_sock 
{
	my ($str) = @_;
	chomp($str);
	send($socket, $str."\r\n", 0);
	print("[YOU] ".$str."\n");
}

sub config 
{
	my ($block, $name) = @_;
	$block = lc($block);
	$name = lc($name);
	if (defined $settings->{$block}->{$name}) 
	{
		return $settings->{$block}->{$name};
	} else 
	{
		return 0;
	}
}

sub error 
{
	my ($type, $msg) = @_;
	print("[ERROR] ".$msg."\n");
	my ($file);
	if ($type ne 0) 
	{
		$type = lc($type);
#		log($type, $msg);
	}
	exit;
}	
